#!/bin/zsh

# environment variables, aliases and functions
if [ -f "$HOME/.shrc" ]; then
	. "$HOME/.shrc"
fi

# emacs mode
bindkey -e

setopt hist_ignore_all_dups no_share_history

# use modern completion system
autoload -Uz compinit
compinit

# enable color variables
autoload -Uz colors
colors

# make alt-backspace / ctrl+W behave like bash
autoload -Uz select-word-style
select-word-style bash

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=2
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'


#	 /$$$$$$$   /$$$$$$    /$$  
#	| $$__  $$ /$$__  $$ /$$$$  
#	| $$  \ $$| $$  \__/|_  $$  
#	| $$$$$$$/|  $$$$$$   | $$  
#	| $$____/  \____  $$  | $$  
#	| $$       /$$  \ $$  | $$  
#	| $$      |  $$$$$$/ /$$$$$$
#	|__/       \______/ |______/

# wrap in a function to allow local variables
_set_ps1() {
	local user_color='green'
	local host_color='green'
	local host_str='%m%k'

	if [ $USER = 'vagrant' ]; then
		user_color='blue'
	elif [ $USER = 'root' ]; then
		user_color='red'
	elif [ $USER != 'andreas' ]; then
		user_color='yellow'
	fi

	if [ -n "$SSH_CLIENT" ]; then
		host_str=$(hostname -f 2>/dev/null || hostname)
		if [[ $host_str == vagrant* ]]; then
			host_color='blue'
		else
			host_color='yellow'
		fi
	fi

	PS1="%{$fg_bold[$user_color]%}%n%{$fg_no_bold[white]%}@%{$fg_bold[$host_color]%}$host_str %{$reset_color%}%~"

	if [ -z "$debian_chroot" ] && [ -r /etc/debian_chroot ]; then
		debian_chroot=$(cat /etc/debian_chroot)
	fi
	if [ ! -z "$debian_chroot" ]; then
		PS1="${debian_chroot:+($debian_chroot)} $PS1"
	fi

	PS1+=$'\n'"%{$fg_no_bold[white]%}%%%{$reset_color%} "
	export PS1
}

_set_ps1
